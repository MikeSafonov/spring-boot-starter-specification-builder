plugins {
    id "java"
    id "jacoco"
    id "io.spring.dependency-management" version "1.0.11.RELEASE"
    id 'info.solidsoft.pitest' version '1.6.0'
    id "org.sonarqube" version "3.3"
}

apply plugin: "java-library"
apply plugin: "maven"
apply plugin: "signing"

ext { springBootVersion = "2.7.2" }

group "com.github.mikesafonov"
version "1.10.0"

sourceCompatibility = 1.8
[compileJava, compileTestJava]*.options*.encoding = "UTF-8"

repositories {
    mavenCentral()
}

dependencyManagement {
    imports { mavenBom("org.springframework.boot:spring-boot-dependencies:${springBootVersion}") }
}

jar {
    enabled = true
}

configurations {
    compile.exclude module: "spring-boot-starter-logging"
}

dependencies {
    implementation ("org.springframework.boot:spring-boot-starter-data-jpa")

    implementation ("org.slf4j:slf4j-api")
    implementation ("org.apache.commons:commons-lang3")

    compileOnly ("org.projectlombok:lombok:1.18.20")
    annotationProcessor ("org.projectlombok:lombok:1.18.20")

    testCompileOnly ("org.projectlombok:lombok:1.18.16")
    testAnnotationProcessor ("org.projectlombok:lombok:1.18.16")
    testImplementation ("org.springframework.boot:spring-boot-starter-test")
    testImplementation ("com.h2database:h2:1.4.200")
}

task sourceJar(type: Jar) {
    classifier "sources"
    from sourceSets.main.allJava
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier "javadoc"
    from javadoc.destinationDir
}

artifacts {
    archives sourceJar
    archives javadocJar
}

signing {
    sign configurations.archives
}

jacoco {
    toolVersion = "0.8.3"
}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

jacocoTestReport {
    reports {
        xml.enabled true
        csv.enabled false
    }
}

// Build, sign, and upload
uploadArchives {
    repositories {
        mavenDeployer {

            // Sign POM
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            // Destination
            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2") {
                authentication(userName: sonatypeUsername, password: sonatypePassword)
            }

            // Add required metadata to POM
            pom.project {
                name "spring-boot-starter-specification-builder"
                packaging "jar"
                description "Spring Boot starter for building specifications in declarative way"
                url "https://github.com/MikeSafonov/spring-boot-starter-specification-builder"
                organization {
                    name "com.github.mikesafonov"
                    url "https://github.com/MikeSafonov"
                }
                issueManagement {
                    system "GitHub"
                    url "https://github.com/MikeSafonov/spring-boot-starter-specification-builder/issues"
                }
                licenses {
                    license {
                        name "MIT"
                        url "https://github.com/MikeSafonov/spring-boot-starter-specification-builder/blob/master/LICENSE"
                        distribution "repo"
                    }
                }
                scm {
                    url "https://github.com/MikeSafonov/spring-boot-starter-specification-builder"
                    connection "scm:git:git://github.com/MikeSafonov/spring-boot-starter-specification-builder.git"
                    developerConnection "scm:git:ssh://git@github.com:MikeSafonov/spring-boot-starter-specification-builder.git"
                }
                developers {
                    developer {
                        name "Mike Safonov"
                        organization "com.github.mikesafonov"
                        organizationUrl "https://github.com/MikeSafonov"
                    }
                }
            }
        }
    }
}

pitest {
    junit5PluginVersion = '0.12'
    targetClasses = ["com.github.mikesafonov.specification.builder.starter.*"]
    threads = 4
    outputFormats = ["HTML", "XML"]
    enableDefaultIncrementalAnalysis = true
    timestampedReports = false
    historyInputLocation = ".pitest/pitHistory.txt"
    historyOutputLocation = ".pitest/pitHistory.txt"
}


sonarqube {
    properties {
        property 'sonar.host.url', 'https://sonarcloud.io'
        property 'sonar.organization', 'mikesafonov-github'
        property 'sonar.projectKey', 'MikeSafonov_spring-boot-starter-specification-builder'
        property 'sonar.projectName', 'spring-boot-starter-specification-builder'
        property 'sonar.projectVersion', '1.10.0'
        property 'sonar.sources', 'src/main'
        property 'sonar.tests', 'src/test'
        property 'sonar.coverage.jacoco.xmlReportPaths', 'build/reports/jacoco/test/jacocoTestReport.xml'
        property 'sonar.exclusions', '**/main/resources/**'
        property 'sonar.java.binaries', 'build/classes'
        property 'sonar.links.homepage', 'https://github.com/MikeSafonov/spring-boot-starter-specification-builder'
        property 'sonar.links.ci', 'https://github.com/MikeSafonov/spring-boot-starter-specification-builder'
        property 'sonar.links.scm', 'https://github.com/MikeSafonov/spring-boot-starter-specification-builder'
        property 'sonar.links.issue', 'https://github.com/MikeSafonov/spring-boot-starter-specification-builder/issues'
    }
}
